/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

class Global {
    static def POM_NAME = 'android-sdk'
    static def POM_ARTIFACT_ID = 'android-sdk'
    static def POM_PACKAGING = 'aar'
    static def VERSION_NAME = '1.4.4.3'
    static def GROUP_ID = 'com.izooto'
    static def POM_DESCRIPTION = 'Push Notification Services'
    static def POM_URL = 'https://github.com/izooto-mobile-sdk/android-X'
    static def POM_SCM_URL = 'https://github.com/izooto-mobile-sdk/android-X'
    static def POM_SCM_CONNECTION = 'scm:git:https://github.com/izooto-mobile-sdk/android-X'
    static def POM_SCM_DEV_CONNECTION = 'scm:git:https://github.com/izooto-mobile-sdk/android-X'
    static def POM_LICENCE_NAME = 'MIT License'
    static def POM_LICENCE_URL='https://raw.githubusercontent.com/izooto-mobile-sdk/android-X/master/LICENSE'
    static def POM_LICENCE_DIST = 'android-X'
    static def POM_DEVELOPER_ID = 'iZootoAndroidSDK'
    static def POM_DEVELOPER_NAME = 'Amit Kumar Gupta'
    static def SNAPSHOT_REPOSITORY_URL = 'https://oss.sonatype.org/content/repositories/snapshots'
    static def RELEASE_REPOSITORY_URL = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
}
def isReleaseBuild() {
    return VERSION_NAME.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = Global.GROUP_ID
                pom.artifactId = Global.POM_ARTIFACT_ID
                pom.version = Global.VERSION_NAME

                repository(url: getReleaseRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }
                snapshotRepository(url: getSnapshotRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name Global.POM_NAME
                    packaging Global.POM_PACKAGING
                    description Global.POM_DESCRIPTION
                    url Global.POM_URL

                    scm {
                        url Global.POM_SCM_URL
                        connection Global.POM_SCM_CONNECTION
                        developerConnection Global.POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name Global.POM_LICENCE_NAME
                            url Global.POM_LICENCE_URL
                            distribution Global.POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id Global.POM_DEVELOPER_ID
                            name Global.POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }


    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
    }
}